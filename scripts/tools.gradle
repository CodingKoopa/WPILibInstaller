configurations {
  wpiLibTools
}

dependencies {
  wpiLibTools 'edu.wpi.first.shuffleboard:app:+'
  wpiLibTools 'edu.wpi.first.wpilib:OutlineViewer:+'
  wpiLibTools 'edu.wpi.first.wpilib:SmartDashboard:+'
  wpiLibTools 'edu.wpi.first.wpilib:RobotBuilder:+'
}

ext.toolsConfig = {
  def toolsFolder = 'tools'
  return new Tuple2({ task->
    task.inputs.property 'toolsFolder', toolsFolder
  }, { config->
    config['ToolsFolder'] = toolsFolder
  })
}

ext.toolsSetup = { Zip zip->
  configurations.wpiLibTools.files.each {
    if (it.name.startsWith('app')) {
      zip.from (it) {
        into '/tools'
        rename { 'ShuffleBoard.jar' }
      }
    } else {
      def str = it.name.substring(0, it.name.indexOf('-')) + '.jar'
      zip.from (it) {
        into '/tools'
        rename { str }
      }
    }
  }
}
