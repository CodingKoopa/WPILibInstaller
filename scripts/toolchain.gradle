def fileName = 'windows-toolchain-2019-1.zip'
def gccVersion = '6.3.0'
def baseUrl = 'https://github.com/wpilibsuite/toolchain-builder/releases/download/v2019-1/'

def downloadUrl = baseUrl + fileName

def extractLocation = file("$buildDir/toolchain")

apply plugin: 'de.undercouch.download'

def downloadTask = tasks.register('downloadToolchain', Download) {
  src downloadUrl
  dest buildDir
  overwrite false
}

def extractTask = tasks.register('extractToolchain', Copy) {
  dependsOn downloadTask

  from zipTree(file("$buildDir/$fileName")).matching{ include "frc2019/roborio/**"}
  into extractLocation
  eachFile { f->
    f.path = f.path.substring(16) // length of frc2019/roborio/
  }

  includeEmptyDirs = false
}

ext.toolchainConfigTaskSetup = {
  return new Tuple2({ task->
    task.inputs.property 'toolchainName', fileName
  }, { config->
    def toolchainConfig = [:];

    toolchainConfig['Version'] = gccVersion
    toolchainConfig['Directory'] = 'roborio'

    config['CppToolchain'] = toolchainConfig
  });
}

ext.toolchainZipSetup = { Zip zip->
  zip.dependsOn extractTask

  zip.inputs.dir extractLocation

  zip.from(extractLocation) {
    into '/roborio'
  }
}
